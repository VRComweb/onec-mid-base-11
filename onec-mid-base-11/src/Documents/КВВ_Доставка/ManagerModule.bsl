#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт	
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Анкета доставки
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АнкетаДоставки";
	КомандаПечати.Представление = НСтр("ru = 'Анкета клиента о доставке'");
	КомандаПечати.Порядок = 5;
	
	// ТоварнаяНакладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТоварнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Товарная накладная'");
	КомандаПечати.Порядок = 15;

	// Комплект Анкета и ТоварнаяНакладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АнкетаДоставки,ТоварнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов'");
	КомандаПечати.Порядок = 25;
	
	// Договор на доставку DOCx
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "УправлениеПечатью";
	КомандаПечати.Идентификатор = "Документ.КВВ_Доставка.ПФ_DOC_ДоговорНаДоставку";
	КомандаПечати.Представление = НСтр("ru = 'Договор на доставку (DOCx)'");
	КомандаПечати.Порядок = 35;

	
КонецПроцедуры



Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АнкетаДоставки");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАнкетаДоставки(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Анкета клиента о доставке'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.КВВ_Доставка.ПФ_MXL_АнкетаДоставки";
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ТоварнаяНакладная");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьТоварнаяНакладная(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Товарная накладная'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.КВВ_Доставка.ПФ_MXL_ТоварнаяНакладная";
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти




#Область СлужебныеПроцедурыИФункции



Функция ПечатьАнкетаДоставки(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АнкетаДоставки";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.КВВ_Доставка.ПФ_MXL_АнкетаДоставки");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиАнкетаДоставки(ДанныеДокументов, ТабличныйДокумент, Макет);
		
        // В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ВывестиАнкетаДоставки(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьШапкаАнкеты = Макет.ПолучитьОбласть("ШапкаАнкеты");
	
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(ДанныеДокументов.Ссылка, 1, 120);
	Если НЕ ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
    	ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
                |Технические подробности см. в журнале регистрации.'");
    	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	Иначе
    	КартинкаQRКода = Новый Картинка(ДанныеQRКода);
    	ОбластьШапкаАнкеты.Рисунки.QRКод.Картинка = КартинкаQRКода;
	КонецЕсли;
	
//	ОбластьШапкаАнкеты.Параметры.Заполнить(ДанныеДокументов);
	ОбластьШапкаАнкеты.Параметры.Номер = Число(ДанныеДокументов.Номер);
	ОбластьШапкаАнкеты.Параметры.Дата = Формат(ДанныеДокументов.Дата, "ДФ=dd.MM.yyyy");
	
	ТабличныйДокумент.Вывести(ОбластьШапкаАнкеты);
	
КонецПроцедуры


Функция ПечатьТоварнаяНакладная(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ТоварнаяНакладная";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.КВВ_Доставка.ПФ_MXL_ТоварнаяНакладная");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиТоварнаяНакладная(ДанныеДокументов, ТабличныйДокумент, Макет);
		
        // В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ВывестиТоварнаяНакладная(ДанныеДокументов, ТабличныйДокумент, Макет)
	

	ОбластьШапкаТН = Макет.ПолучитьОбласть("ШапкаТН");
	ОбластьШапкаТЧ = Макет.ПолучитьОбласть("ШапкаТЧ");
	ОбластьСтрокаТЧ = Макет.ПолучитьОбласть("СтрокаТЧ");
	ОбластьПодвалТН = Макет.ПолучитьОбласть("ПодвалТН");
	
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(ДанныеДокументов.Ссылка, 1, 120);
	Если НЕ ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
    	ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
                |Технические подробности см. в журнале регистрации.'");
    	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	Иначе
    	КартинкаQRКода = Новый Картинка(ДанныеQRКода);
    	ОбластьШапкаТН.Рисунки.QRКод.Картинка = КартинкаQRКода;
	КонецЕсли;
	
//	ОбластьШапкаАнкеты.Параметры.Заполнить(ДанныеДокументов);
	ОбластьШапкаТН.Параметры.Номер = Число(ДанныеДокументов.Номер);
	ОбластьШапкаТН.Параметры.Дата = Формат(ДанныеДокументов.Дата, "ДФ=dd.MM.yyyy");
	ОбластьШапкаТН.Параметры.Грузоотправитель = ДанныеДокументов.Контрагент;
	ОбластьШапкаТН.Параметры.Грузополучатель = ДанныеДокументов.Организация;
	
	
	ТабличныйДокумент.Вывести(ОбластьШапкаТН);
	ТабличныйДокумент.Вывести(ОбластьШапкаТЧ);
	
	Выборка = ДанныеДокументов.Товары.Выгрузить();
	Для Каждого Строка Из Выборка Цикл
		
		ОбластьСтрокаТЧ.Параметры.Номер = Строка.НомерСтроки;
		ОбластьСтрокаТЧ.Параметры.Товар = Строка.Номенклатура;
		ОбластьСтрокаТЧ.Параметры.Количество = Строка.Количество;
		
		ТабличныйДокумент.Вывести(ОбластьСтрокаТЧ);
	КонецЦикла;

	ТабличныйДокумент.Вывести(ОбластьПодвалТН);
	
КонецПроцедуры



Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КВВ_Доставка.Ссылка КАК Ссылка,
	               |	КВВ_Доставка.ВерсияДанных КАК ВерсияДанных,
	               |	КВВ_Доставка.ПометкаУдаления КАК ПометкаУдаления,
	               |	КВВ_Доставка.Номер КАК Номер,
	               |	КВВ_Доставка.Дата КАК Дата,
	               |	КВВ_Доставка.Проведен КАК Проведен,
	               |	КВВ_Доставка.Организация КАК Организация,
	               |	КВВ_Доставка.Контрагент КАК Контрагент,
	               |	КВВ_Доставка.Договор КАК Договор,
	               |	КВВ_Доставка.АдресДоставки КАК АдресДоставки,
	               |	КВВ_Доставка.Основание КАК Основание,
	               |	КВВ_Доставка.Ответственный КАК Ответственный,
	               |	КВВ_Доставка.Комментарий КАК Комментарий,
	               |	КВВ_Доставка.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество
	               |	) КАК Товары,
	               |	КВВ_Доставка.Представление КАК Представление,
	               |	КВВ_Доставка.МоментВремени КАК МоментВремени
	               |ИЗ
	               |	Документ.КВВ_Доставка КАК КВВ_Доставка
	               |ГДЕ
	               |	КВВ_Доставка.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции



#КонецОбласти









#КонецЕсли